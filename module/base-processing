// Modified by Eric Yates.
// Modifications:
//    Create columns for brightness, temperature, humidity, and acceleration.
//    Add conditional if statement to ensure only full-length strings are captured.
//    Fix minor bugs and typos.

/* 
  Saving Values from Arduino to a .csv File Using Processing - Pseduocode

  This sketch provides a basic framework to read data from Arduino over the serial port and save it to .csv file on your computer.
  The .csv file will be saved in the same folder as your Processing sketch.
  This sketch takes advantage of Processing 2.0's built-in Table class.
  This sketch assumes that values read by Arduino are separated by commas, and each Arduino reading is separated by a newline character.
  Each reading will have it's own row and timestamp in the resulting csv file. 
  This sketch will write a new file a set number of times. Each file will contain all records from the beginning of the sketch's run.  
  This sketch pseduo-code only. Comments will direct you to places where you should customize the code.
  This is a beginning level sketch.

  The hardware:
  * Sensors connected to Arduino input pins
  * Arduino connected to computer via USB cord
        
  The software:
  *Arduino programmer
  *Processing (download the Processing software here: https://www.processing.org/download/
  *Download the Software Serial library from here: http://arduino.cc/en/Reference/softwareSerial

  Created 12 November 2014
  By Elaine Laguerta
  http://url/of/online/tutorial.cc

*/

import processing.serial.*;

Serial myPort; //creates a software serial port on which you will listen to Arduino
String val;

Table table; //table where we will read in and store values. You can name it something more creative!

int numReadings = 10000; //keeps track of how many readings you'd like to take before writing the file. 
int readingCounter = 0; //counts each reading to compare to numReadings. 
 
String fileName;

void setup()
{ 
  String portName = Serial.list()[2]; 
  //CAUTION: your Arduino port number is probably different! Mine happened to be 2. 
  //Use a "handshake" sketch to figure out and test which port number your Arduino is talking on. 
  //A "handshake" establishes that Arduino and Processing are listening/talking on the same port.
  //Here's a link to a basic handshake tutorial: https://processing.org/tutorials/overview/
  
  myPort = new Serial(this, portName, 9600); //set up your port to listen to the serial port
  
  table = new Table();
   
  table.addColumn("ID"); //Stores a unique identifier for each record. Count up from 0. First reading ID 0, second ID 1, etc. 
  
  //The following adds columns for time. You can also add milliseconds. 
  //See the Time/Date functions for Processing: https://www.processing.org/reference/ 
  table.addColumn("Year");
  table.addColumn("Month");
  table.addColumn("Day");
  table.addColumn("Hour");
  table.addColumn("Minute");
  table.addColumn("Second");
  table.addColumn("Millisecond");
  
  //the following are dummy columns for each data value. Add as many columns as you have data values. Customize the names as needed. Make sure they are in the same order as the order that Arduino is sending them!
  table.addColumn("Brightness");
  table.addColumn("Temperature");
  table.addColumn("Humidity");
  table.addColumn("x Accel");
  table.addColumn("y Accel");
  table.addColumn("z Accel");
  table.addColumn("x Gyro");
  table.addColumn("y Gyro");
  table.addColumn("z Gyro");

}

void draw()
{
  delay(300);
  val = myPort.readStringUntil('\n'); //The newline separator separates each Arduino loop. We will parse the data by each newline separator. 

  if (val != null) 
  {
  if (val.length() > 60)    // When first creating a connection, partial-strings may be captured and lead to errors. This ensures a full string.
  {
    println(val);
    
    float[] sensorVals = float(split(val, ",")); //parses the packet from Arduino and places the valeus into the sensorVals array. I am assuming floats. Change the data type to match the datatype coming from Arduino. 
   
    TableRow newRow = table.addRow(); //add a row for this new reading
    newRow.setInt("ID", readingCounter);//record a unique identifier (the row's index)
    
    //record time stamp
    newRow.setInt("Year", year());
    newRow.setInt("Month", month());
    newRow.setInt("Day", day());
    newRow.setInt("Hour", hour());
    newRow.setInt("Minute", minute());
    newRow.setInt("Second", second());
    newRow.setInt("Millisecond", millis());
    
    //record sensor information. Customize the names so they match your sensor column names. 
    newRow.setFloat("Brightness", sensorVals[0]);
    newRow.setFloat("Temperature", sensorVals[1]);
    newRow.setFloat("Humidity", sensorVals[2]);
    newRow.setFloat("x Accel", sensorVals[3]);
    newRow.setFloat("y Accel", sensorVals[4]);
    newRow.setFloat("z Accel", sensorVals[5]);
    newRow.setFloat("x Gyro", sensorVals[6]);
    newRow.setFloat("y Gyro", sensorVals[7]);
    newRow.setFloat("z Gyro", sensorVals[8]);
    
    readingCounter++; //optional, use if you'd like to write your file every numReadings reading cycles
    
    //saves the table as a csv in the same folder as the sketch every numReadings. 
    if (readingCounter % numReadings == 0)
    {
      fileName = str(year()) + '-' + str(month()) + '-' + str(day()) + '-' + str(hour()) + '-' + str(minute()) + '-' + str(second()) + ".csv"; //this filename is of the form year+month+day+readingCounter
      saveTable(table, fileName);
      readingCounter = 0;
    }
  }
}
}
